//*********************************************************************************
// * 0.18um Calibre ANTENNA DRC COMMAND FILE - CLM18_LM16_6M.ANT.28a (06/25/2009)
// * FOR TSMC 0.18UM CMOS LOGIC/MS/RF AND 0.16UM CMOS LOGIC/MS 1P6M PROCESS DESIGN RULE*
// *          (CL018G/LV/LP, CM018, CR018 CL016G, CM016)                       *
// * (DESIGN RULE DOCUMENT: T-018-LO-DR-001 Rev2.8)                            *
// * (DRC COMMAND FILE DOCUMENT:  T-018-LO-DR-001-C1 Rev 2.8a)               *
//*********************************************************************************

//	TSMC has developed this deck using Mentor Graphics proprietary SVRF and TVF formats.
//	The deck is to be used  only in Calibre tools
//
//
//  DISCLAIMER
//
//  The information contained herein is provided by TSMC on an "AS IS" basis
//  without any warranty, and TSMC has no obligation to support or otherwise
//  maintain the information.  TSMC disclaims any representation that the
//  information does not infringe any intellectual property rights or proprietary
//  rights of any third parties.  There are no other warranties given by TSMC,
//  whether express, implied or statutory, including, without limitation, implied
//  warranties of merchantability and fitness for a particular purpose.
//
//  STATEMENT OF USE
//
//  This information contains confidential and proprietary information of TSMC.
//  No part of this information may be reproduced, transmitted, transcribed,
//  stored in a retrieval system, or translated into any human or computer
//  language, in any form or by any means, electronic, mechanical, magnetic,
//  optical, chemical, manual, or otherwise, without the prior written permission
//  of TSMC.  This information was prepared for informational purpose and is for
//  use by TSMC's customers only.  TSMC reserves the right to make changes in the
//  information at any time and without notice.
//
//-------------------------------------------------------------------------------------------
//
//  NOTICE: (Read Me First!)
//
//  1. If the input GDS file does not include all the layers in the
//     INPUT LAYER BLOCK, for example, they are generated by logic
//     operations, please make sure the consistence with original
//     layer definition.
//
//  2. The runset is tested and developed on Calibre v2008.4_28.20 (Jan. 2009), please use 
//     this or newer TSMC qualified version of Calibre to execute this runset.
//

//-------------------------------------------------------------------------------------------
//	
//  * To correctly use this DRC deck, the following settings are very important in
//    this DRC command file:
//	
//  (0) Calibre v2008.4_28.20 (Jan. 2009) is used to develop and QA this command file. 
//	Please execute the DRC with this or newer TSMC qualified version of Calibre.
//
//  (1) Variable for Thick Top Metal check:
//
//	By default, the Top Metal is Normal Top Metal. If customer uses 20KA or 40KA Thick Top 
//	Metal, please remove // to the option 'THICK_40K' or 'THICK_20K',But only one should be turn on every time.
//
//	For 20k Top metal:
//		//#DEFINE THICK_40K
//		#DEFINE THICK_20K
//
//	For 40k Top metal:
//		#DEFINE THICK_40K
//		//#DEFINE THICK_20K

//  (2) Variable for 2fF/um2 check:
//
//       By default, the 2fF/um2 MIMCAP is used. If customer don't use 2fF/um2 MIMCAP,
//       Please turn off this switch:
//      
//	       //#DEFINE MIMCAP_2F
//

//-------------------------------------------------------------------------------------------

#DEFINE MIMCAP_2F      // turn on only when customer use 2fF/um2 MIMCAP. otherwise please turn off it

//#DEFINE THICK_40K       // turn on only when 40KA Thick Top Metal is used. otherwise please turn off it
#DEFINE THICK_20K       // turn on only when 20KA Thick Top Metal is used. otherwise please turn off it






VARIABLE M1_THICKNESS 0.53	// The Thickness of First Metal is 5.3K A
VARIABLE M2_THICKNESS 0.53	// The Thickness of Inter Metal is 5.3K A
VARIABLE M3_THICKNESS 0.53	// The Thickness of Inter Metal is 5.3K A
VARIABLE M4_THICKNESS 0.53	// The Thickness of Inter Metal is 5.3K A
VARIABLE M5_THICKNESS 0.53	// The Thickness of Inter Metal is 5.3K A
#IFDEF THICK_40K
VARIABLE M6_THICKNESS 4.6	// The Thickness of Top Metal is 40K A
#ELSE
  #IFDEF THICK_20K
  VARIABLE M6_THICKNESS 2.34	// The Thickness of Top Metal is 20K A

  #ELSE
  VARIABLE M6_THICKNESS 0.99	// The Thickness of Top Metal is 9.9K A
  #ENDIF
#ENDIF

VARIABLE MD_THICKNESS       0.99
VARIABLE PO_THICKNESS	    0.2

VARIABLE Mx_S_1             0.28
VARIABLE Balance_Ratio      6000
VARIABLE unBalance_Ratio     100
VARIABLE DIO_AREA    0.203


//
// ENVIRONMENT SETUP
//------------------
PRECISION    1000
RESOLUTION     5   //Set layout grid  check to 0.005

LAYOUT SYSTEM GDSII
LAYOUT PATH "GDSFILENAME"
LAYOUT PRIMARY "TOPCELLNAME"

DRC RESULTS DATABASE "DRC_RES.db"
DRC SUMMARY REPORT "DRC.rep" 
DRC CHECK TEXT ALL
DRC MAXIMUM RESULTS ALL
//DRC INCREMENTAL CONNECT YES

FLAG OFFGRID YES     // For layout grid check. default grid value is resolution size
FLAG ACUTE YES
FLAG SKEW YES
FLAG NONSIMPLE YES

LAYOUT PROCESS BOX RECORD YES
DRC INCREMENTAL CONNECT YES

// DRAWN LAYER DEFINITIONS
//========================
LAYER  ODi	3 11 12
LAYER  OD2i	4
LAYER  POLYi	13
LAYER  COi	15
LAYER  M1i	16
LAYER  VIA1i	17
LAYER  M2i	18
LAYER  VIA2i	27
LAYER  M3i	28
LAYER  VIA3i	29	// Define connection from M4 to M3
LAYER  M4i	31	// Fourth Metal layer
LAYER  VIA4i	32	// Define connection from M5 to M4
LAYER  M5i	33	// Fifth Metal layer
LAYER  VIA5i	39	// Define connection from MTOP to M5
LAYER  M6i	38	// Sixth Metal layer  
LAYER  VIADi	167	// Define connection from MD to MTOP
LAYER  MDi	168	// RDL Metal layer  

// In TSMC standard mapping, datatype 1 is for dummy metal and datatype 2 is for metal slot

// Metal Slot layers
//--------------------
LAYER MAP 16 DATATYPE 2 321
LAYER  M1SLOTi	321	// Metal1 slot (if drawn)
LAYER MAP 18 DATATYPE 2 322
LAYER  M2SLOTi	322	// Metal2 slot (if drawn)
LAYER MAP 28 DATATYPE 2 323
LAYER  M3SLOTi	323	// Metal3 slot (if drawn)
LAYER MAP 31 DATATYPE 2 324
LAYER  M4SLOTi	324	// Metal4 slot (if drawn)
LAYER MAP 33 DATATYPE 2 325
LAYER  M5SLOTi	325	// Metal5 slot (if drawn)
LAYER MAP 38 DATATYPE 2 326
LAYER  M6SLOTi	326	// Metal6 slot (if drawn)
LAYER MAP 168 DATATYPE 2 328
LAYER  MDSLOTi	328	// MD slot (if drawn)

// Dummy Metal layers
//---------------------
LAYER MAP 16 DATATYPE 1 316
LAYER  M1DMY	316
LAYER MAP 18 DATATYPE 1 318
LAYER  M2DMY	318
LAYER MAP 28 DATATYPE 1 329
LAYER  M3DMY	329
LAYER MAP 31 DATATYPE 1 331
LAYER  M4DMY	331
LAYER MAP 33 DATATYPE 1 333
LAYER  M5DMY	333
LAYER MAP 38 DATATYPE 1 338
LAYER  M6DMY	338
LAYER MAP 168 DATATYPE 1 340
LAYER  MDDMY	340    

// Capacitor Top Metal Layers
//-----------------------------
LAYER MAP 67 DATATYPE 2 257
LAYER CTM2i	257	// Capacitor Top Metal-2
LAYER MAP 67 DATATYPE 3 258
LAYER CTM3i	258	// Capacitor Top Metal-3
LAYER MAP 67 DATATYPE 4 259
LAYER CTM4i	259	// Capacitor Top Metal-4
LAYER MAP 67 DATATYPE 5 260
LAYER CTM5i	260	// Capacitor Top Metal-5

LAYOUT TOP LAYER M1i VIA1i M1SLOTi M1DMY
LAYOUT TOP LAYER M2i VIA2i M2SLOTi M2DMY
LAYOUT TOP LAYER M3i VIA3i M3SLOTi M3DMY
LAYOUT TOP LAYER M4i VIA4i M4SLOTi M4DMY
LAYOUT TOP LAYER M5i VIA5i M5SLOTi M5DMY
LAYOUT TOP LAYER MDi VIADi MDSLOTi MDDMY M6i M6SLOTi M6DMY

// Metal in silicon
//-------------------
M1 = (M1i NOT M1SLOTi) OR M1DMY
M2 = (M2i NOT M2SLOTi) OR M2DMY
M3 = (M3i NOT M3SLOTi) OR M3DMY
M4 = (M4i NOT M4SLOTi) OR M4DMY
M5 = (M5i NOT M5SLOTi) OR M5DMY
M6 = (M6i NOT M6SLOTi) OR M6DMY
MD = (MDi NOT MDSLOTi) OR MDDMY








// Exclude the via upon CTM region
//----------------------------------
VIAt = VIA5i NOT INTERACT CTM5i
VIAc = VIA5i AND CTM5i
GATE = ODi AND POLYi
SD   = ODi NOT POLYi

CTM = COPY CTM5i
CBM = M5 AND (SIZE CTM BY 2 INSIDE OF M5 STEP Mx_S_1 * 0.7)

VIA_CTM = VIA5i AND CTM
VIA_CBM = (VIA5i NOT CTM) AND CBM
VIA_NO_CTM_CBM = (VIA5i NOT CTM) NOT CBM

// POLY CHECKS
//==============

CONNECT POLYi GATE

A.R.1.POLY { @ (Poly perim*thickness / gate area) > 200 
  NET AREA RATIO POLYi GATE > 200
    [
      PERIMETER(POLYi)*0.2/AREA(GATE)
    ] RDB A.R.1.POLYi.rep POLYi GATE
}

// CO CHECKS
//============

CONNECT COi POLYi

A.R.4.CO { @ (contact area / gate area) > 10 
  CO_POLYi = COi AND POLYi
  NET AREA RATIO POLYi CO_POLYi GATE > 10   // error highlight on POLY
    [
      AREA(CO_POLYi)/AREA(GATE) + AREA(POLYi)/AREA(POLYi) - 1
    ] RDB A.R.4.CO.rep CO_POLYi GATE  
}

// M1 CHECKS
//============

CONNECT M1 POLYi SD BY COi

M1_DIOD = NET AREA RATIO SD M1 > 0
M1_MIN_DIO = NET AREA M1_DIOD > 0.203		// minimum effective diode 0.203

A.R.2_A.R.3.M1 { @ (M1 perim*thickness / gate area) > 400 (without effective diode) 
		 @ (M1 perim*thickness / gate area) > Ratio (with effective diode) 
  NET AREA RATIO M1 M1_MIN_DIO GATE > 0		// no effective diode
    [
      !!AREA(M1) * !!AREA(GATE) *
      (!AREA(M1_MIN_DIO) * ( PERIMETER(M1)*M1_THICKNESS/AREA(GATE) - 400 ) +
      !!AREA(M1_MIN_DIO) * ( PERIMETER(M1)*M1_THICKNESS/AREA(GATE) - AREA(M1_MIN_DIO)*400 - 2200 ))
    ] RDB A.R.2.M1.rep M1 M1_MIN_DIO GATE
}

// VIA1 CHECKS
//==============
CONNECT VIA1i M1

VIA1_DIO = NET AREA RATIO SD VIA1i > 0
VIA1_MIN_DIO = NET AREA VIA1_DIO > 0.203	// minimum effective diode 0.203

A.R.5_A.R.6.VIA1i { @ (VIA1 area / gate area) > 20 (without effective diode)
                    @ (VIA1 area / gate area) > Ratio (with effective diode)
   NET AREA RATIO M1 VIA1i VIA1_MIN_DIO GATE > 0	// no effective diode
    [
      !!AREA(M1) * !!AREA(GATE) * 
      (!AREA(VIA1_MIN_DIO) * ( AREA(VIA1i)/AREA(GATE) - 20 ) +       
      !!AREA(VIA1_MIN_DIO) * ( AREA(VIA1i)/AREA(GATE) - AREA(VIA1_MIN_DIO)*83.33 - 75 ))
    ] RDB A.R.5_A.R.6.VIA1.rep VIA1i VIA1_MIN_DIO GATE
}

// M2 CHECKS
//============

CONNECT M2 M1 BY VIA1i

M2_DIOD = NET AREA RATIO SD M2 > 0
M2_MIN_DIO = NET AREA M2_DIOD > 0.203		// minimum effective diode 0.203

A.R.2_A.R.3.M2 { @ (M2 perim*thickness / gate area) > 400 (without effective diode) 
		 @ (M2 perim*thickness / gate area) > Ratio (with effective diode) 
  NET AREA RATIO M2 M2_MIN_DIO GATE > 0		// no effective diode
    [
      !!AREA(M2) * !!AREA(GATE) *
      (!AREA(M2_MIN_DIO) * ( PERIMETER(M2)*M2_THICKNESS/AREA(GATE) - 400 ) +
      !!AREA(M2_MIN_DIO) * ( PERIMETER(M2)*M2_THICKNESS/AREA(GATE) - AREA(M2_MIN_DIO)*400 - 2200 ))
    ] RDB A.R.2.M2.rep M2 M2_MIN_DIO GATE
}

// VIA2 CHECKS
//==============
CONNECT VIA2i M2

VIA2_DIO = NET AREA RATIO SD VIA2i > 0
VIA2_MIN_DIO = NET AREA VIA2_DIO > 0.203	// minimum effective diode 0.203

A.R.5_A.R.6.VIA2i { @ (VIA2 area / gate area) > 20 (without effective diode)
                    @ (VIA2 area / gate area) > Ratio (with effective diode)
   NET AREA RATIO M2 VIA2i VIA2_MIN_DIO GATE > 0	// no effective diode
    [
      !!AREA(M2) * !!AREA(GATE) * 
      (!AREA(VIA2_MIN_DIO) * ( AREA(VIA2i)/AREA(GATE) - 20 ) +       
      !!AREA(VIA2_MIN_DIO) * ( AREA(VIA2i)/AREA(GATE) - AREA(VIA2_MIN_DIO)*83.33 - 75 ))
    ] RDB A.R.5_A.R.6.VIA2.rep VIA2i VIA2_MIN_DIO GATE
}

// M3 CHECKS
//============

CONNECT M3 M2 BY VIA2i

M3_DIOD = NET AREA RATIO SD M3 > 0
M3_MIN_DIO = NET AREA M3_DIOD > 0.203		// minimum effective diode 0.203

A.R.2_A.R.3.M3 { @ (M3 perim*thickness / gate area) > 400 (without effective diode) 
		 @ (M3 perim*thickness / gate area) > Ratio (with effective diode) 
  NET AREA RATIO M3 M3_MIN_DIO GATE > 0		// no effective diode
    [
      !!AREA(M3) * !!AREA(GATE) *
      (!AREA(M3_MIN_DIO) * ( PERIMETER(M3)*M3_THICKNESS/AREA(GATE) - 400 ) +
      !!AREA(M3_MIN_DIO) * ( PERIMETER(M3)*M3_THICKNESS/AREA(GATE) - AREA(M3_MIN_DIO)*400 - 2200 ))
    ] RDB A.R.2.M3.rep M3 M3_MIN_DIO GATE
}

// VIA3 CHECKS
//==============
CONNECT VIA3i M3

VIA3_DIO = NET AREA RATIO SD VIA3i > 0
VIA3_MIN_DIO = NET AREA VIA3_DIO > 0.203	// minimum effective diode 0.203

A.R.5_A.R.6.VIA3i { @ (VIA3 area / gate area) > 20 (without effective diode)
                    @ (VIA3 area / gate area) > Ratio (with effective diode)
   NET AREA RATIO M3 VIA3i VIA3_MIN_DIO GATE > 0	// no effective diode
    [
      !!AREA(M3) * !!AREA(GATE) * 
      (!AREA(VIA3_MIN_DIO) * ( AREA(VIA3i)/AREA(GATE) - 20 ) +       
      !!AREA(VIA3_MIN_DIO) * ( AREA(VIA3i)/AREA(GATE) - AREA(VIA3_MIN_DIO)*83.33 - 75 ))
    ] RDB A.R.5_A.R.6.VIA3.rep VIA3i VIA3_MIN_DIO GATE
}

// M4 CHECKS
//============

CONNECT M4 M3 BY VIA3i

M4_DIOD = NET AREA RATIO SD M4 > 0
M4_MIN_DIO = NET AREA M4_DIOD > 0.203		// minimum effective diode 0.203

A.R.2_A.R.3.M4 { @ (M4 perim*thickness / gate area) > 400 (without effective diode) 
		 @ (M4 perim*thickness / gate area) > Ratio (with effective diode) 
  NET AREA RATIO M4 M4_MIN_DIO GATE > 0		// no effective diode
    [
      !!AREA(M4) * !!AREA(GATE) *
      (!AREA(M4_MIN_DIO) * ( PERIMETER(M4)*M4_THICKNESS/AREA(GATE) - 400 ) +
      !!AREA(M4_MIN_DIO) * ( PERIMETER(M4)*M4_THICKNESS/AREA(GATE) - AREA(M4_MIN_DIO)*400 - 2200 ))
    ] RDB A.R.2.M4.rep M4 M4_MIN_DIO GATE
}

// VIA4 CHECKS
//==============
CONNECT VIA4i M4

VIA4_DIO = NET AREA RATIO SD VIA4i > 0
VIA4_MIN_DIO = NET AREA VIA4_DIO > 0.203	// minimum effective diode 0.203

A.R.5_A.R.6.VIA4i { @ (VIA4 area / gate area) > 20 (without effective diode)
                    @ (VIA4 area / gate area) > Ratio (with effective diode)
   NET AREA RATIO M4 VIA4i VIA4_MIN_DIO GATE > 0	// no effective diode
    [
      !!AREA(M4) * !!AREA(GATE) * 
      (!AREA(VIA4_MIN_DIO) * ( AREA(VIA4i)/AREA(GATE) - 20 ) +       
      !!AREA(VIA4_MIN_DIO) * ( AREA(VIA4i)/AREA(GATE) - AREA(VIA4_MIN_DIO)*83.33 - 75 ))
    ] RDB A.R.5_A.R.6.VIA4.rep VIA4i VIA4_MIN_DIO GATE
}

// M5 CHECKS
//============

CONNECT M5 M4 BY VIA4i

M5_DIOD = NET AREA RATIO SD M5 > 0
M5_MIN_DIO = NET AREA M5_DIOD > 0.203		// minimum effective diode 0.203

A.R.2_A.R.3.M5 { @ (M5 perim*thickness / gate area) > 400 (without effective diode) 
		 @ (M5 perim*thickness / gate area) > Ratio (with effective diode) 
  NET AREA RATIO M5 M5_MIN_DIO GATE > 0		// no effective diode
    [
      !!AREA(M5) * !!AREA(GATE) *
      (!AREA(M5_MIN_DIO) * ( PERIMETER(M5)*M5_THICKNESS/AREA(GATE) - 400 ) +
      !!AREA(M5_MIN_DIO) * ( PERIMETER(M5)*M5_THICKNESS/AREA(GATE) - AREA(M5_MIN_DIO)*400 - 2200 ))
    ] RDB A.R.2.M5.rep M5 M5_MIN_DIO GATE
}

// VIA5 CHECKS
//==============
CONNECT CBM M5

#IFDEF MIMCAP_2F
A.R.MIM.5.M5_CBM { @ Mx as MIM capacitor bottom metal directly connecting to OD
                   @ through underneath VIA is not allowed
  NET AREA RATIO CBM SD > 0
     RDB A.R.MIM.5.M5.CBM.rep CBM SD
}
#ENDIF
CONNECT VIAt M5

VIA5_DIO = NET AREA RATIO SD VIAt > 0
VIA5_MIN_DIO = NET AREA VIA5_DIO > 0.203	// minimum effective diode 0.203

A.R.5_A.R.6.VIAt { @ (VIA5 area / gate area) > 20 (without effective diode)
                    @ (VIA5 area / gate area) > Ratio (with effective diode)
   NET AREA RATIO M5 VIAt VIA5_MIN_DIO GATE > 0	// no effective diode
    [
      !!AREA(M5) * !!AREA(GATE) * 
      (!AREA(VIA5_MIN_DIO) * ( AREA(VIAt)/AREA(GATE) - 20 ) +       
      !!AREA(VIA5_MIN_DIO) * ( AREA(VIAt)/AREA(GATE) - AREA(VIA5_MIN_DIO)*83.33 - 75 ))
    ] RDB A.R.5_A.R.6.VIA5.rep VIAt VIA5_MIN_DIO GATE
}


// M6 CHECKS
//============
//============
CONNECT CBM M5
CTMc = COPY CTM
CONNECT VIA_CTM CTM
CONNECT VIA_CBM CBM
CONNECT CTMc CBM

CONNECT M6 CTM CBM M5 BY VIA5i

M6_DIOD = NET AREA RATIO SD M6 > 0
M6_MIN_DIO = NET AREA M6_DIOD > 0.203		// minimum effective diode 0.203

A.R.2_A.R.3.M6 { @ (M6 perim*thickness / gate area) > 400 (without effective diode) 
                 @ (M6 perim*thickness / gate area) > Ratio (with effective diode) 
  NET AREA RATIO M6 M6_MIN_DIO GATE > 0		// no effective diode
    [
      !!AREA(M6) * !!AREA(GATE) *
      (!AREA(M6_MIN_DIO) * ( PERIMETER(M6)*M6_THICKNESS/AREA(GATE) - 400 ) +
      !!AREA(M6_MIN_DIO) * ( PERIMETER(M6)*M6_THICKNESS/AREA(GATE) - AREA(M6_MIN_DIO)*8000 - 30000 ))
    ] RDB A.R.2.M6.rep M6 M6_MIN_DIO GATE
}



CTM_M6_WI_OD = NET AREA RATIO CTM SD > 0
CTM_M6_WO_OD = CTM NOT CTM_M6_WI_OD
CTM_M6_W_GATE_pre = NET AREA RATIO CTM GATE > 0
CTM_M6_W_GATE = CTM_M6_W_GATE_pre AND CTM_M6_WO_OD
CTM_M6_FLOATING = CTM NOT (CTM_M6_WI_OD OR CTM_M6_W_GATE)

CBM_M6_WI_OD = NET AREA RATIO CBM SD > 0
CBM_M6_WO_OD = CBM NOT CBM_M6_WI_OD
CBM_M6_W_GATE = NET AREA RATIO CBM GATE > 0
CBM_M6_FLOATING = CBM NOT (CBM_M6_WI_OD OR CBM_M6_W_GATE)

BAL_CTM_M6_GROUND = CTM_M6_WI_OD INTERACT CBM_M6_WI_OD
BAL_CBM_M6_GROUND = CBM_M6_WI_OD INTERACT CTM_M6_WI_OD
BAL_CTM_M6_FLOAT  = CTM_M6_FLOATING INTERACT CBM_M6_FLOATING
BAL_CBM_M6_FLOAT  = CBM_M6_FLOATING INTERACT CTM_M6_FLOATING

BAL_CTM_M6   = BAL_CTM_M6_GROUND OR BAL_CTM_M6_FLOAT
BAL_CBM_M6   = BAL_CBM_M6_GROUND OR BAL_CBM_M6_FLOAT
unBAL_CTM_M6 = CTM NOT INTERACT BAL_CTM_M6
unBAL_CBM_M6 = CBM NOT INTERACT BAL_CBM_M6
unBAL_CTM_M6_FLOAT  = CTM_M6_WO_OD INTERACT unBAL_CTM_M6
unBAL_CTM_M6_GROUND = CTM_M6_WI_OD INTERACT unBAL_CTM_M6
unBAL_CBM_M6_FLOAT  = CBM_M6_WO_OD INTERACT unBAL_CBM_M6
unBAL_CBM_M6_GROUND = CBM_M6_WI_OD INTERACT unBAL_CBM_M6

CTM_NODE_BAL_M6_WI_OD = CTM INTERACT BAL_CTM_M6_GROUND
CTM_NODE_BAL_M6_WO_OD = CTM INTERACT BAL_CTM_M6_FLOAT
CONNECT CTM_NODE_BAL_M6_WI_OD CTM
CONNECT CTM_NODE_BAL_M6_WO_OD CTM
CBM_NODE_BAL_M6_WI_OD = CTM INTERACT BAL_CBM_M6_GROUND
CBM_NODE_BAL_M6_WO_OD = CTM INTERACT BAL_CBM_M6_FLOAT
CONNECT CBM_NODE_BAL_M6_WI_OD CBM
CONNECT CBM_NODE_BAL_M6_WO_OD CBM
CTM_NODE_unBAL_M6_WI_OD = CTM INTERACT unBAL_CTM_M6_GROUND
CTM_NODE_unBAL_M6_WO_OD = CTM INTERACT unBAL_CTM_M6_FLOAT
CONNECT CTM_NODE_unBAL_M6_WI_OD CTM
CONNECT CTM_NODE_unBAL_M6_WO_OD CTM
CBM_NODE_unBAL_M6_WI_OD = CTM INTERACT unBAL_CBM_M6_GROUND
CBM_NODE_unBAL_M6_WO_OD = CTM INTERACT unBAL_CBM_M6_FLOAT
CONNECT CBM_NODE_unBAL_M6_WI_OD CBM
CONNECT CBM_NODE_unBAL_M6_WO_OD CBM
M6_DIO = NET AREA SD >= DIO_AREA

A.R.MIM.1.M6.CBM { @ Maximum ratio of M6 perimeter area to the MIM cap. when
                      @ CTM is not connected to OD and
                      @ Mx as MIM capacitor metal bottom metal is not connected to OD < ^Balance_Ratio
   NET AREA RATIO M6 CBM_NODE_BAL_M6_WO_OD > Balance_Ratio ACCUMULATE
   [!!AREA(CBM_NODE_BAL_M6_WO_OD)*(PERIMETER(M6)*M6_THICKNESS/AREA(CBM_NODE_BAL_M6_WO_OD))]
     RDB A.R.MIM.1.M6.CBM.rep M6 CBM_NODE_BAL_M6_WO_OD BY LAYER
}

A.R.MIM.1.M6.CTM { @ Maximum ratio of M6 perimeter area to the MIM cap. when
                      @ CTM is not connected to OD and
                      @ Mx as MIM capacitor metal bottom metal is not connected to OD < ^Balance_Ratio
   NET AREA RATIO M6 CTM_NODE_BAL_M6_WO_OD > Balance_Ratio ACCUMULATE
   [!!AREA(CTM_NODE_BAL_M6_WO_OD)*(PERIMETER(M6)*M6_THICKNESS/AREA(CTM_NODE_BAL_M6_WO_OD))]
     RDB A.R.MIM.1.M6.CTM.rep M6 CTM_NODE_BAL_M6_WO_OD BY LAYER
}

A.R.MIM.2.M6.CTM.A { @ Unbalanced MIM structure:
                      @ CTM is connceted to OD with OD area < ^DIO_AREA and
                      @ Mx as MIM capacitor metal bottom metal is not connected to OD
  NET AREA RATIO M6 CTM_NODE_unBAL_M6_WI_OD SD > 0 ACCUMULATE
      [!!AREA(CTM_NODE_unBAL_M6_WI_OD) * (DIO_AREA - AREA(SD))]
      RDB A.R.MIM.2.M6.CTM.WO_DIO.rep M6 CTM_NODE_unBAL_M6_WI_OD SD BY LAYER
}

A.R.MIM.2.M6.CTM.B { @ Maximum ratio of the metal area to the MIM cap. when
                      @ CTM is connceted to OD and
                      @ Mx as MIM capacitor metal bottom metal is not connected to OD
  NET AREA RATIO M6 M6_DIO CTM_NODE_unBAL_M6_WI_OD > 0 ACCUMULATE		      
  [!!AREA(CTM_NODE_unBAL_M6_WI_OD)*!!AREA(M6_DIO)*(PERIMETER(M6)*M6_THICKNESS/AREA(CTM_NODE_unBAL_M6_WI_OD) - AREA(M6_DIO)*400 - 2200 )]
     RDB A.R.MIM.2.M6.CTM.WI_DIO.rep M6 CTM_NODE_unBAL_M6_WI_OD M6_DIO BY LAYER
}

A.R.MIM.2.M6.CBM { @ Maximum ratio of the metal area to the MIM cap. when
                      @ CTM is connceted to OD and
                      @ Mx as MIM capacitor metal bottom metal is not connected to OD < ^unBalance_Ratio
  NET AREA RATIO M6 CBM_NODE_unBAL_M6_WO_OD > unBalance_Ratio ACCUMULATE
  [!!AREA(CBM_NODE_unBAL_M6_WO_OD)*(PERIMETER(M6)*M6_THICKNESS/AREA(CBM_NODE_unBAL_M6_WO_OD))]
     RDB A.R.MIM.2.M6.CBM.rep M6 CBM_NODE_unBAL_M6_WO_OD BY LAYER
}

A.R.MIM.3.M6.CTM { @ Maximum ratio of the metal area to the MIM cap. when
                      @ CTM is not connceted to OD and
                      @ Mx as MIM capacitor metal bottom metal is connected to OD < ^unBalance_Ratio
  NET AREA RATIO M6 CTM_NODE_unBAL_M6_WO_OD > unBalance_Ratio ACCUMULATE
  [!!AREA(CTM_NODE_unBAL_M6_WO_OD)*(PERIMETER(M6)*M6_THICKNESS/AREA(CTM_NODE_unBAL_M6_WO_OD))]
     RDB A.R.MIM.3.M6.CTM.rep M6 CTM_NODE_unBAL_M6_WO_OD BY LAYER
}

A.R.MIM.3.M6.CBM.A { @ Unbalanced MIM structure:
                      @ CTM is not connceted to OD and
                      @ Mx as MIM capacitor metal bottom metal is connected to OD with OD area < ^DIO_AREA
  NET AREA RATIO M6 CBM_NODE_unBAL_M6_WI_OD SD > 0 ACCUMULATE
     [!!AREA(CBM_NODE_unBAL_M6_WI_OD) * (DIO_AREA - AREA(SD))]
     RDB A.R.MIM.3.M6.CBM.WO_DIO.rep M6 CBM_NODE_unBAL_M6_WI_OD SD BY LAYER
}

A.R.MIM.3.M6.CBM.B { @ Maximum ratio of the metal area to the MIM cap. when
                      @ CTM is not connceted to OD and
                      @ Mx as MIM capacitor metal bottom metal is connected to OD
  NET AREA RATIO M6 M6_DIO CBM_NODE_unBAL_M6_WI_OD > 0 ACCUMULATE		      
  [!!AREA(CBM_NODE_unBAL_M6_WI_OD)*!!AREA(M6_DIO)*(PERIMETER(M6)*M6_THICKNESS/AREA(CBM_NODE_unBAL_M6_WI_OD) - AREA(M6_DIO)*400 - 2200 )]
     RDB A.R.MIM.3.M6.CBM.WI_DIO.rep M6 CBM_NODE_unBAL_M6_WI_OD M6_DIO BY LAYER
 }

A.R.MIM.4.M6.CTM.A { @ Balanced MIM structure but the OD area connected to CTM < ^DIO_AREA
  NET AREA RATIO M6 CTM_NODE_BAL_M6_WI_OD SD > 0 ACCUMULATE
    [!!AREA(CTM_NODE_BAL_M6_WI_OD) * (DIO_AREA - AREA(SD))]
     RDB A.R.MIM.4.M6.CTM.WO_DIO.rep M6 CTM_NODE_BAL_M6_WI_OD SD BY LAYER
}

A.R.MIM.4.M6.CTM.B { @ Max. ratio of M6 metal side-wall area to CTM area.
                @ Both CTM and CBM associated M5 are connected to OD.
  NET AREA RATIO M6 M6_DIO CTM_NODE_BAL_M6_WI_OD > 0 ACCUMULATE
  [ !!AREA(CTM_NODE_BAL_M6_WI_OD) * ( (PERIMETER(M6) * M6_THICKNESS) / AREA(CTM_NODE_BAL_M6_WI_OD) - AREA(M6_DIO)*400 - 2200 )
    ] RDB A.R.MIM.4.M6.CTM.WI_DIO.rep M6 CTM_NODE_BAL_M6_WI_OD M6_DIO BY LAYER
}

A.R.MIM.4.M6.CBM.A { @ Balanced MIM structure but the OD area connected to Mx as capacitor bottom metal < ^DIO_AREA
  NET AREA RATIO M6 CBM_NODE_BAL_M6_WI_OD SD > 0 ACCUMULATE
    [!!AREA(CBM_NODE_BAL_M6_WI_OD) * (DIO_AREA - AREA(SD))]
     RDB A.R.MIM.4.M6.CBM.WO_DIO.rep M6 CBM_NODE_BAL_M6_WI_OD SD BY LAYER
}

A.R.MIM.4.M6.CBM.B { @ Max. ratio of M6 metal side-wall area to CBM area.
                @ Both CTM and CBM associated M5 are connected to OD.
  NET AREA RATIO M6 M6_DIO CBM_NODE_BAL_M6_WI_OD > 0 ACCUMULATE		    
  [
      !!AREA(CBM_NODE_BAL_M6_WI_OD) * ( (PERIMETER(M6) * M6_THICKNESS) / AREA(CBM_NODE_BAL_M6_WI_OD) - AREA(M6_DIO)*400 - 2200 )
    ] RDB A.R.MIM.4.CBM.M6.WI_DIO.rep M6 CBM_NODE_BAL_M6_WI_OD M6_DIO BY LAYER
}

#IFDEF MIMCAP_2F
A.R.MIM.5.M6_CTM { @ Below condition is not allowed for 2fF/um2 MIM capacitance
                      @ CTM is not connected to OD and
                      @ M6 as capacitor bottom metal is connected to OD
  NET AREA RATIO CTM_NODE_unBAL_M6_WO_OD M6 SD > 0
      [!!AREA(CTM_NODE_unBAL_M6_WO_OD)*!!AREA(M6)*!AREA(SD)]
}
A.R.MIM.5.M6_CBM { @ Below condition is not allowed for 2fF/um2 MIM capacitance
                      @ CTM is connected to OD and
                      @ M6 as capacitor bottom metal is not connected to OD
  NET AREA RATIO CBM_NODE_unBAL_M6_WO_OD M6 SD > 0
      [!!AREA(CBM_NODE_unBAL_M6_WO_OD)*!!AREA(M6)*!AREA(SD)]
}
#ENDIF

ACC_M6_CTM  = NET AREA RATIO M6 CTM  > 0 ACCUMULATE
                     [!!AREA(M6) * PERIMETER(M6) * M6_THICKNESS / AREA(CTM)]
ACC_M6_CTMc = NET AREA RATIO M6 CTMc > 0 ACCUMULATE
                     [!!AREA(M6) * PERIMETER(M6) * M6_THICKNESS / AREA(CTMc)]

#IFNDEF THICK_40K
#IFNDEF THICK_20K
		     
// VIAD CHECKS
//==============

CONNECT VIADi M6

VIAD_DIO = NET AREA RATIO SD VIADi > 0
VIAD_MIN_DIO = NET AREA VIAD_DIO > 0.203	// minimum effective diode 0.203

A.R.5_A.R.6.VIAD { @ (VIAD area / gate area) > 20 (without effective diode) 
	 	   @ (VIAD area / gate area) > Ratio (with effective diode) 
  NET AREA RATIO M6 VIADi VIAD_MIN_DIO GATE > 0	// no effective diode
    [
      !!AREA(M6) * !!AREA(GATE) * 
      (!AREA(VIAD_MIN_DIO) * ( AREA(VIADi)/AREA(GATE) - 20 ) +       
      !!AREA(VIAD_MIN_DIO) * ( AREA(VIADi)/AREA(GATE) - AREA(VIAD_MIN_DIO)*83.33 - 75 ))	
    ] RDB A.R.5_A.R.6.VIAD.rep VIADi VIAD_MIN_DIO GATE
}
		     

CONNECT MD M6 BY VIADi

CTM_MD_WI_OD = NET AREA RATIO CTM SD > 0
CTM_MD_WO_OD = CTM NOT CTM_MD_WI_OD
CTM_MD_W_GATE_pre = NET AREA RATIO CTM GATE > 0
CTM_MD_W_GATE = CTM_MD_W_GATE_pre AND CTM_MD_WO_OD
CTM_MD_FLOATING = CTM NOT (CTM_MD_WI_OD OR CTM_MD_W_GATE)

CBM_MD_WI_OD = NET AREA RATIO CBM SD > 0
CBM_MD_WO_OD = CBM NOT CBM_MD_WI_OD
CBM_MD_W_GATE = NET AREA RATIO CBM GATE > 0
CBM_MD_FLOATING = CBM NOT (CBM_MD_WI_OD OR CBM_MD_W_GATE)

BAL_CTM_MD_GROUND = CTM_MD_WI_OD INTERACT CBM_MD_WI_OD
BAL_CBM_MD_GROUND = CBM_MD_WI_OD INTERACT CTM_MD_WI_OD
BAL_CTM_MD_FLOAT  = CTM_MD_FLOATING INTERACT CBM_MD_FLOATING
BAL_CBM_MD_FLOAT  = CBM_MD_FLOATING INTERACT CTM_MD_FLOATING

BAL_CTM_MD   = BAL_CTM_MD_GROUND OR BAL_CTM_MD_FLOAT
BAL_CBM_MD   = BAL_CBM_MD_GROUND OR BAL_CBM_MD_FLOAT
unBAL_CTM_MD = CTM NOT INTERACT BAL_CTM_MD
unBAL_CBM_MD = CBM NOT INTERACT BAL_CBM_MD
unBAL_CTM_MD_FLOAT  = CTM_MD_WO_OD INTERACT unBAL_CTM_MD
unBAL_CTM_MD_GROUND = CTM_MD_WI_OD INTERACT unBAL_CTM_MD
unBAL_CBM_MD_FLOAT  = CBM_MD_WO_OD INTERACT unBAL_CBM_MD
unBAL_CBM_MD_GROUND = CBM_MD_WI_OD INTERACT unBAL_CBM_MD

CTM_NODE_BAL_MD_WI_OD = CTM INTERACT BAL_CTM_MD_GROUND
CTM_NODE_BAL_MD_WO_OD = CTM INTERACT BAL_CTM_MD_FLOAT
CONNECT CTM_NODE_BAL_MD_WI_OD CTM
CONNECT CTM_NODE_BAL_MD_WO_OD CTM
CBM_NODE_BAL_MD_WI_OD = CTM INTERACT BAL_CBM_MD_GROUND
CBM_NODE_BAL_MD_WO_OD = CTM INTERACT BAL_CBM_MD_FLOAT
CONNECT CBM_NODE_BAL_MD_WI_OD CBM
CONNECT CBM_NODE_BAL_MD_WO_OD CBM
CTM_NODE_unBAL_MD_WI_OD = CTM INTERACT unBAL_CTM_MD_GROUND
CTM_NODE_unBAL_MD_WO_OD = CTM INTERACT unBAL_CTM_MD_FLOAT
CONNECT CTM_NODE_unBAL_MD_WI_OD CTM
CONNECT CTM_NODE_unBAL_MD_WO_OD CTM
CBM_NODE_unBAL_MD_WI_OD = CTM INTERACT unBAL_CBM_MD_GROUND
CBM_NODE_unBAL_MD_WO_OD = CTM INTERACT unBAL_CBM_MD_FLOAT
CONNECT CBM_NODE_unBAL_MD_WI_OD CBM
CONNECT CBM_NODE_unBAL_MD_WO_OD CBM
MD_DIO = NET AREA SD >= DIO_AREA

A.R.MIM.1.MD.CBM { @ Maximum ratio of MD perimeter area to the MIM cap. when
                      @ CTM is not connected to OD and
                      @ Mx as MIM capacitor metal bottom metal is not connected to OD < ^Balance_Ratio
  A = NET AREA RATIO MD M6 CBM_NODE_BAL_MD_WO_OD CTMc > Balance_Ratio ACCUMULATE ACC_M6_CTMc
    [!!AREA(CBM_NODE_BAL_MD_WO_OD)*(PERIMETER(MD)*MD_THICKNESS/AREA(CBM_NODE_BAL_MD_WO_OD))]
     RDB A.R.MIM.1.MD.CBM.rep MD CBM_NODE_BAL_MD_WO_OD BY LAYER
     A INTERACT CBM_NODE_BAL_MD_WO_OD
}

A.R.MIM.1.MD.CTM { @ Maximum ratio of MD perimeter area to the MIM cap. when
                      @ CTM is not connected to OD and
                      @ Mx as MIM capacitor metal bottom metal is not connected to OD < ^Balance_Ratio
  A = NET AREA RATIO MD M6 CTM_NODE_BAL_MD_WO_OD CTM > Balance_Ratio ACCUMULATE ACC_M6_CTM
     [!!AREA(CTM_NODE_BAL_MD_WO_OD)*(PERIMETER(MD)*MD_THICKNESS/AREA(CTM_NODE_BAL_MD_WO_OD))]
     RDB A.R.MIM.1.MD.CTM.rep MD CTM_NODE_BAL_MD_WO_OD BY LAYER
  A INTERACT CTM_NODE_BAL_MD_WO_OD
}

A.R.MIM.2.MD.CTM.A { @ Unbalanced MIM structure:
                      @ CTM is connceted to OD with OD area < ^DIO_AREA and
                      @ Mx as MIM capacitor metal bottom metal is not connected to OD
  NET AREA RATIO MD CTM_NODE_unBAL_MD_WI_OD SD > 0 ACCUMULATE
      [!!AREA(CTM_NODE_unBAL_MD_WI_OD) * (DIO_AREA - AREA(SD))]
      RDB A.R.MIM.2.MD.CTM.WO_DIO.rep MD CTM_NODE_unBAL_MD_WI_OD SD BY LAYER
}

A.R.MIM.2.MD.CTM.B { @ Maximum ratio of the metal area to the MIM cap. when
                      @ CTM is connceted to OD and
                      @ Mx as MIM capacitor metal bottom metal is not connected to OD
  A = NET AREA RATIO MD M6 CTM_NODE_unBAL_MD_WI_OD MD_DIO CTM > 0 ACCUMULATE ACC_M6_CTM
  [!!AREA(CTM_NODE_unBAL_MD_WI_OD)*!!AREA(MD_DIO)*(PERIMETER(MD)*MD_THICKNESS/AREA(CTM_NODE_unBAL_MD_WI_OD) - AREA(MD_DIO)*400 - 2200 )]
     RDB A.R.MIM.2.MD.CTM.WI_DIO.rep MD CTM_NODE_unBAL_MD_WI_OD MD_DIO BY LAYER
  A INTERACT CTM_NODE_unBAL_MD_WI_OD
}

A.R.MIM.2.MD.CBM { @ Maximum ratio of the metal area to the MIM cap. when
                      @ CTM is connceted to OD and
                      @ Mx as MIM capacitor metal bottom metal is not connected to OD < ^unBalance_Ratio
  A = NET AREA RATIO MD M6 CBM_NODE_unBAL_MD_WO_OD CTMc > unBalance_Ratio ACCUMULATE ACC_M6_CTMc
  
    [!!AREA(CBM_NODE_unBAL_MD_WO_OD)*(PERIMETER(MD)*MD_THICKNESS/AREA(CBM_NODE_unBAL_MD_WO_OD))]
     RDB A.R.MIM.2.MD.CBM.rep MD CBM_NODE_unBAL_MD_WO_OD BY LAYER
  A INTERACT CBM_NODE_unBAL_MD_WO_OD
}

A.R.MIM.3.MD.CTM { @ Maximum ratio of the metal area to the MIM cap. when
                      @ CTM is not connceted to OD and
                      @ Mx as MIM capacitor metal bottom metal is connected to OD < ^unBalance_Ratio
  A = NET AREA RATIO MD M6 CTM_NODE_unBAL_MD_WO_OD CTM > unBalance_Ratio ACCUMULATE ACC_M6_CTM
  [!!AREA(CTM_NODE_unBAL_MD_WO_OD)*(PERIMETER(MD)*MD_THICKNESS/AREA(CTM_NODE_unBAL_MD_WO_OD))]
     RDB A.R.MIM.3.MD.CTM.rep MD CTM_NODE_unBAL_MD_WO_OD BY LAYER
  A INTERACT CTM_NODE_unBAL_MD_WO_OD
}

A.R.MIM.3.MD.CBM.A { @ Unbalanced MIM structure:
                      @ CTM is not connceted to OD and
                      @ Mx as MIM capacitor metal bottom metal is connected to OD with OD area < ^DIO_AREA
  NET AREA RATIO MD CBM_NODE_unBAL_MD_WI_OD SD > 0 ACCUMULATE
     [!!AREA(CBM_NODE_unBAL_MD_WI_OD) * (DIO_AREA - AREA(SD))]
     RDB A.R.MIM.3.MD.CBM.WO_DIO.rep MD CBM_NODE_unBAL_MD_WI_OD SD BY LAYER
}

A.R.MIM.3.MD.CBM.B { @ Maximum ratio of the metal area to the MIM cap. when
                      @ CTM is not connceted to OD and
                      @ Mx as MIM capacitor metal bottom metal is connected to OD
  A = NET AREA RATIO MD M6 CBM_NODE_unBAL_MD_WI_OD MD_DIO CTMc > 0 ACCUMULATE ACC_M6_CTMc
  
    [!!AREA(CBM_NODE_unBAL_MD_WI_OD)*!!AREA(MD_DIO)*(PERIMETER(MD)*MD_THICKNESS/AREA(CBM_NODE_unBAL_MD_WI_OD) - AREA(MD_DIO)*400 - 2200 )]
     RDB A.R.MIM.3.MD.CBM.WI_DIO.rep MD CBM_NODE_unBAL_MD_WI_OD MD_DIO BY LAYER
  A INTERACT CBM_NODE_unBAL_MD_WI_OD
 }

A.R.MIM.4.MD.CTM.A { @ Balanced MIM structure but the OD area connected to CTM < ^DIO_AREA
  NET AREA RATIO MD CTM_NODE_BAL_MD_WI_OD SD > 0 ACCUMULATE
    [!!AREA(CTM_NODE_BAL_MD_WI_OD) * (DIO_AREA - AREA(SD))]
     RDB A.R.MIM.4.MD.CTM.WO_DIO.rep MD CTM_NODE_BAL_MD_WI_OD SD BY LAYER
}

A.R.MIM.4.MD.CTM.B { @ Max. ratio of M6 metal side-wall area to CTM area.
                @ Both CTM and CBM associated M5 are connected to OD.
  A = NET AREA RATIO MD CTM_NODE_BAL_MD_WI_OD MD_DIO CTM > 0 ACCUMULATE ACC_M6_CTM
  
    [ !!AREA(CTM_NODE_BAL_MD_WI_OD) * ( (PERIMETER(MD) * MD_THICKNESS) / AREA(CTM_NODE_BAL_MD_WI_OD) - AREA(MD_DIO)*400 - 2200 )
    ] RDB A.R.MIM.4.MD.CTM.WI_DIO.rep MD CTM_NODE_BAL_MD_WI_OD MD_DIO BY LAYER
  A INTERACT CTM_NODE_BAL_MD_WI_OD
}

A.R.MIM.4.MD.CBM.A { @ Balanced MIM structure but the OD area connected to Mx as capacitor bottom metal < ^DIO_AREA
  NET AREA RATIO MD CBM_NODE_BAL_MD_WI_OD SD > 0 ACCUMULATE
    [!!AREA(CBM_NODE_BAL_MD_WI_OD) * (DIO_AREA - AREA(SD))]
     RDB A.R.MIM.4.MD.CBM.WO_DIO.rep MD CBM_NODE_BAL_MD_WI_OD SD BY LAYER
}

A.R.MIM.4.MD.CBM.B { @ Max. ratio of M6 metal side-wall area to CBM area.
                @ Both CTM and CBM associated M5 are connected to OD.
  A = NET AREA RATIO MD CBM_NODE_BAL_MD_WI_OD MD_DIO CTMc > 0 ACCUMULATE ACC_M6_CTMc
  
    [
      !!AREA(CBM_NODE_BAL_MD_WI_OD) * ( (PERIMETER(MD) * MD_THICKNESS) / AREA(CBM_NODE_BAL_MD_WI_OD) - AREA(MD_DIO)*400 - 2200 )
    ] RDB A.R.MIM.4.CBM.MD.WI_DIO.rep MD CBM_NODE_BAL_MD_WI_OD MD_DIO BY LAYER
  A INTERACT CBM_NODE_BAL_MD_WI_OD
}

#IFDEF MIMCAP_2F
A.R.MIM.5.MD_CTM { @ Below condition is not allowed for 2fF/um2 MIM capacitance
                      @ CTM is not connected to OD and
                      @ MD as capacitor bottom metal is connected to OD
  NET AREA RATIO CTM_NODE_unBAL_MD_WO_OD MD SD > 0
      [!!AREA(CTM_NODE_unBAL_MD_WO_OD)*!!AREA(MD)*!AREA(SD)]
}
A.R.MIM.5.MD_CBM { @ Below condition is not allowed for 2fF/um2 MIM capacitance
                      @ CTM is connected to OD and
                      @ MD as capacitor bottom metal is not connected to OD
  NET AREA RATIO CBM_NODE_unBAL_MD_WO_OD MD SD > 0
      [!!AREA(CBM_NODE_unBAL_MD_WO_OD)*!!AREA(MD)*!AREA(SD)]
}
#ENDIF




// MD CHECKS
//============


MD_DIOD = NET AREA RATIO SD MD > 0
MD_MIN_DIO = NET AREA MD_DIOD > 0.203		// minimum effective diode 0.203

A.R.2_A.R.3.MD { @ (MD perim*thickness / gate area) > 400 (without effective diode) 
		 @ (MD perim*thickness / gate area) > Ratio (with effective diode) 
  NET AREA RATIO MD MD_MIN_DIO GATE > 0		// no effective diode
    [
      !!AREA(MD) * !!AREA(GATE) *
      (!AREA(MD_MIN_DIO) * ( PERIMETER(MD)*MD_THICKNESS/AREA(GATE) - 400 ) +
      !!AREA(MD_MIN_DIO) * ( PERIMETER(MD)*MD_THICKNESS/AREA(GATE) - AREA(MD_MIN_DIO)*8000 - 30000 ))
    ] RDB A.R.2.MD.rep MD MD_MIN_DIO GATE
}

#ENDIF
#ENDIF

DISCONNECT
